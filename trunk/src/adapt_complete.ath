  Author: Jens-Christian Korth
  Copyright: Jens-Christian Korth
  License: see files SOFTWARE-LICENSE, PATENT-LICENSE

brick #adapt_complete
purpose Complete shorter requests
desc
  If a request returns less bytes than requested,
  this brick automatically repeats the operation
  for the missing bytes.
enddesc

static_data {
#include <string.h>
}


input :<in


output :>out


operation $output_init
{
  @success = TRUE;
}


operation $trans,$transwait
{

  // NOTE: Using "@=call ... &myargs" is a bad ATHOMUX programming style.
  //       For new code, please use "@=call ... (...) => (...)" if possible!
  //       "@=call ... & a r g s" should only be used for argument forwarding.

  struct args myargs;
  plen_t mylen = 0;

  memcpy(&myargs, @args, sizeof(myargs));
  do {
    myargs.success = FALSE;
    @=call :<in$OP_NAME &myargs;
    if(myargs.success) {
      mylen += myargs.phys_len;
      if(myargs.log_len > myargs.phys_len) {
	myargs.log_len   -= myargs.phys_len;
	myargs.log_addr  += myargs.phys_len;
	myargs.phys_addr += myargs.phys_len;
      } else {
	break;
      }
    } else {
      break;
    }
  } while(1);

  if(mylen) {
    @success   = TRUE;
    @phys_len = mylen;
  }
}


operation $op
{
  switch(@op_code) {
  case opcode_trans:
    @=call $trans @args;
    break;
  case opcode_transwait:
    @=call $transwait @args;
    break;
  case opcode_output_init:
    @success = TRUE;
    break;
  default:
    @=call :<in$op @args;
  }
}
